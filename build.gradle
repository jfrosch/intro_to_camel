group 'com.jackfrosch.camel'
version '1.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.8 // Camel 2.14 onwards requires JDK 1.7 or better

repositories {
    jcenter()
    mavenCentral()
}
// TODO: Review Gradle docs for setting up sourceSets
sourceSets {
    integrationTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/groovy')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime

    all*.exclude group: "org.slf4j", module: "slf4j-simple"
    all*.exclude group: "log4j", module: "log4j"
}

idea {
    module {
        //add integration test source dirs to IDEA
        testSourceDirs += file('src/integrationTest/groovy')
        scopes.TEST.plus += [ configurations.integrationTestCompile ]
        downloadJavadoc = false
        downloadSources = false
    }
}

dependencies {
    //It needs to be one of the first dependencies to work
    compile 'ch.qos.logback:logback-classic:1.1.6'

    compile 'org.codehaus.groovy:groovy-all:2.3.11'

    // Camel dependencies
    def camelVersion = '2.16.2'
    compile "org.apache.camel:camel-core:$camelVersion"
    compile "org.apache.camel:camel-groovy:$camelVersion"
    compile "org.apache.camel:camel-csv:$camelVersion"

    testCompile "org.apache.camel:camel-test:$camelVersion"

    testCompile 'junit:junit:4.11'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'cglib:cglib-nodep:2.2'
}